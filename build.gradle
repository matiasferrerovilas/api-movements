buildscript {
    dependencies {
        classpath("org.liquibase:liquibase-core:5.0.1")
    }
}
plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.liquibase.gradle' version '3.0.2'
    id 'checkstyle'
}

group = 'api.expenses'
version = '1.2.1'

java {
    sourceCompatibility = '25'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

checkstyle {
    configFile = file("$rootDir/checkstyle/checkstyle.xml")
    toolVersion = "12.1.2"
}

checkstyleMain {
    source = 'src/main/java'
}

ext {
    mapstructVersion = '1.6.3'
    testcontainersVersion = '1.21.3'
}

dependencies {
    // Spring Boot Starters
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    testAnnotationProcessor "org.springframework.boot:spring-boot-starter-webmvc-test"

    implementation 'org.springframework.boot:spring-boot-starter-validation'
    testAnnotationProcessor 'org.springframework.boot:spring-boot-starter-validation-test'

    implementation 'org.springframework.boot:spring-boot-starter-aspectj'
    implementation 'org.springframework.boot:spring-boot-starter-aspectj-test'

    implementation 'org.springframework.boot:spring-boot-starter-cache'
    testAnnotationProcessor 'org.springframework.boot:spring-boot-starter-cache-test'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    testAnnotationProcessor 'org.springframework.boot:spring-boot-data-jpa-test'

    implementation 'org.springframework.boot:spring-boot-starter-security'
    testAnnotationProcessor 'org.springframework.boot:spring-boot-starter-security-test'

    implementation 'org.springframework.boot:spring-boot-starter-oauth2-authorization-server'
    testAnnotationProcessor 'org.springframework.boot:spring-boot-starter-security-oauth2-authorization-server-test'

    implementation 'org.springframework.boot:spring-boot-starter-security-oauth2-client'
    testAnnotationProcessor 'org.springframework.boot:spring-boot-starter-security-oauth2-client-test'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    //WebSocket
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    testAnnotationProcessor 'org.springframework.boot:spring-boot-starter-websocket-test'

    // OpenAPI/Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.0'

    //Logs
    implementation 'org.apache.commons:commons-text:1.14.0'

    // Cache
    implementation 'com.github.ben-manes.caffeine:caffeine'

    // Database
    runtimeOnly 'com.mysql:mysql-connector-j:9.5.0'

    // Liquibase
    implementation 'org.liquibase:liquibase-core'
    liquibaseRuntime 'org.liquibase:liquibase-core'
    liquibaseRuntime 'info.picocli:picocli:4.7.7'
    liquibaseRuntime 'com.mysql:mysql-connector-j:9.5.0'
    liquibaseRuntime 'org.apache.commons:commons-lang3:3.20.0'

    // Lombok + MapStruct (orden importante)
    compileOnly "org.mapstruct:mapstruct:${mapstructVersion}"
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    // Observability
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    testAnnotationProcessor "org.springframework.boot:spring-boot-starter-actuator-test"

    // PDF
    implementation 'org.apache.pdfbox:pdfbox:3.0.6'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'

    // Testcontainers
    testImplementation "org.testcontainers:junit-jupiter:${testcontainersVersion}"
    testImplementation "org.testcontainers:mysql:${testcontainersVersion}"

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.mockito:mockito-core:5.20.0'


}

tasks.named('test') {
    useJUnitPlatform()

    // Configuración de encoding
    systemProperty 'file.encoding', 'UTF-8'

    // Performance
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1

    // Logging mejorado
    testLogging {
        events 'passed', 'skipped', 'failed'
        exceptionFormat 'full'
        showStandardStreams = false
        showCauses = true
        showStackTraces = true
    }
}
tasks.register('printVersion') {
    doLast {
        print project.version
    }
}
tasks.withType(Test).configureEach {
    reports.html.required = false
    reports.junitXml.required = false
}

springBoot {
    buildInfo()
}


// Configuración de compilación
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs += [
            '-parameters',
            '-Xlint:unchecked',
            '-Xlint:deprecation'
    ]
}

liquibase {
    def dbName = System.properties['dbName'] ?: 'expenses'
    activities {
        main {
            changelogFile 'src/main/resources/db/changelog/changelog.yaml'
            url "jdbc:mysql://192.168.1.100:3306/$dbName"
            username 'root'
            password 'helios'
        }
    }
}
